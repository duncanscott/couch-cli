apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'maven'

sourceCompatibility = 1.7

repositories {
	mavenCentral()
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Couch-Cli', 'Implementation-Version': version
	}
}

mainClassName = 'net.duncanscott.couch_cli.client.CouchClient'

task wrapper(type:Wrapper) {
	gradleVersion = '2.4'
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.3'
	compile 'log4j:log4j:1.2.17'
	compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
	compile 'commons-cli:commons-cli:1.3.1'
	testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

processResources {
	filesMatching("**/CouchClientConfig.groovy") {
		expand project.properties
	}
}

configurations {
	unitTestCompile.extendsFrom testCompile
	unitTestRuntime.extendsFrom testRuntime
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
	main {
		groovy {
			srcDir 'src/main/groovy'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
	unitTest {
		groovy {
			srcDir 'src/test/unit/groovy'
		}
		resources {
			srcDir 'src/test/unit/resources'
		}
		compileClasspath += sourceSets.main.output
		runtimeClasspath = output + compileClasspath
	}
	integrationTest {
		groovy {
			srcDir 'src/test/integration/groovy'
		}
		resources {
			srcDir 'src/test/integration/resources'
		}
		compileClasspath += sourceSets.main.output
		runtimeClasspath = output + compileClasspath
	}
}

task unitTest(type:Test) {
	binResultsDir = file("$buildDir/$name-results/binary")
	reports {
		html.destination = file("$buildDir/reports/$name")
		junitXml.destination = binResultsDir.parentFile
	}
	classpath = sourceSets.unitTest.runtimeClasspath
	testClassesDir = sourceSets.unitTest.output.classesDir
}

task integrationTest(type:Test) {
	mustRunAfter unitTest
	binResultsDir = file("$buildDir/$name-results/binary")
	reports {
		html.destination = file("$buildDir/reports/$name")
		junitXml.destination = binResultsDir.parentFile
	}
	classpath = sourceSets.integrationTest.runtimeClasspath
	testClassesDir = sourceSets.integrationTest.output.classesDir
}

test.dependsOn integrationTest, unitTest

test {
	testLogging {
		// set options for log level LIFECYCLE
		events "failed"
		exceptionFormat "short"
		// set options for log level DEBUG
		debug {
			events "started", "skipped", "failed"
			exceptionFormat "full"
		}
	}
}

tasks.withType(Test) { testTask ->
	beforeTest { TestDescriptor descriptor->
		String msg = "running ${testTask.name}: ${descriptor.className} \"${descriptor.name}\""
		logger.warn msg
	}
	afterTest { TestDescriptor descriptor, TestResult result ->
		long duration = result.endTime - result.startTime
		String logMessage = "${result.resultType} (${duration} msecs): ${testTask.name} ${descriptor.className} \"${descriptor.name}\""
		switch (result.resultType) {
			case TestResult.ResultType.SUCCESS:
				logger.info "${logMessage}"
				break;
			case TestResult.ResultType.FAILURE:
				logger.error "${logMessage}"
				break;
			case TestResult.ResultType.SKIPPED:
				logger.warn "${logMessage}"
				break;
			default:
				logger.warn "${logMessage}"
		}
	}
}
